
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

// Velson artifact descriptors
group = 'com.github.drexler'
archivesBaseName = "velson"
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
   compile(
     [group: 'commons-lang', name: 'commons-lang', version: '2.6'],
     [group: 'commons-collections', name: 'commons-collections', version: '3.2.2'],
     [group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.12'],
     [group: 'org.apache.velocity', name: 'velocity-tools', version: '2.0'],
     [group: 'com.jayway.jsonpath', name: 'json-path', version: '2.3.0'],
     [group: 'commons-io', name: 'commons-io', version: '2.5']
   )
   // Custom velocity 1.7 build.
   compile fileTree(dir: 'libs', include: '*.jar')
   runtime(
     [group: 'commons-digester', name: 'commons-digester', version: '2.1']
   )
   testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    from {
        (configurations.compile).collect {
            it.isDirectory() ? it : zipTree(it)
        }

        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes("Main-Class": "com.drexler.velson.Velson" )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

sourceSets {
  main {
    resources {
      srcDir 'src/main/java'
    }
  }
  test {
    java.srcDir 'src/test/java'
  }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

// Signature of artifacts
signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'velson'
        packaging 'jar'

        description 'A simple tool imitating AWS transformation of JSON using Velocity templates'
        url 'https://github.com/drexler/velson'

        scm {
          connection 'scm:git:https://github.com/drexler/velson.git'
          developerConnection 'scm:git:ssh://github.com:drexler/velson.git'
          url 'https://github.com/drexler/velson.git'
        }

        licenses {
          license {
            name 'The MIT License (MIT)'
            url 'https://github.com/drexler/velson/blob/master/LICENSE.md'
          }
        }

        developers {
          developer {
            id 'drexler'
            name 'Andrew Quartey'
          }
        }
      }
    }
  }
}

mainClassName = 'com.drexler.velson.Velson'
